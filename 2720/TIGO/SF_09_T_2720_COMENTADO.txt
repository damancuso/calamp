//0000 Nota: OJO, Tener cuidado con los espacios al final de linea en el script porque el equipo interpreta mal agregando ceros!!!!!

AT$APP DEFAULT ALL						//Setea por defecto todos los parametros, S-Registers y PEG Events.

AT$APP GPRS CONTEXT 0 "skycop.tigo.py"	//APN 0
AT$APP GPRS CONTEXT 1 "skycop.tigo.py"	//APN 1
AT$APP PPP USER "tigo"					//Usuario
AT$APP PPP PASSWORD "tigo"				//Contraseña
AT$APP MOBID USER						//Usar ID personalizado
AT$APP MOBID USER 000000				//ID "000000", una vez finalizada la programacion se envia el comando
										//		       "AT$APP MOBID USER 123456" con el ID correspondiente.
AT$APP INBOUND 172.17.9.230:5052		//IP y Puerto Servidor

AT$APP PARAM 2319,0,""					//Configuracion URL
AT$APP PARAM 1027,0,30					//Tiempo para declarar perdida de GPS
AT$APP PARAM 1028,0,30					//Tiempo para declarar señal de GPS como última conocida
AT$APP PARAM 2312,0,17					//Configuración mensaje de mantenimiento
AT$APP PARAM 2322,0,15180				//Configura cada cuando se envía mensaje de mantenimiento (15180 s = 4 H)
AT$APP PARAM 771,0,20					//Tiempo de 1º Reinstento mensajes con ACK
AT$APP PARAM 771,1,20					//Tiempo de 2º Reinstento mensajes con ACK
AT$APP PARAM 771,2,0					//Tiempo de 3º Reinstento mensajes con ACK (0 s solo 2 intentos)
AT$APP PARAM 1032,0,2					//Numero de veces que el equipo intenta enviar el LOG cuando la ignición está encendida
AT$APP PARAM 1034,0,2					//Numero de veces que el equipo intenta enviar el LOG cuando la ignición está apagada

AT$APP PARAM 1024,36,1,1				//Habilita el Pinning
at$app param 1280,0,1,1					//Habilita la respuesta a un PING

AT$APP PARAM 1024,7,1,1					//Configura lo que se guarda en caso de reset (Acumuladores)
AT$APP PARAM 1024,7,2,2					//Configura lo que se guarda en caso de reset (Banderas)
AT$APP PARAM 1024,7,64,64				//Configura lo que se guarda en caso de reset (en cualquier reset)
AT$APP PARAM 1024,7,128,128				//Configura lo que se guarda en caso de reset (cuando la Ignición OFF)

//Configura el formato de los paquetes a enviar al server y al LOG
AT$APP PARAM 772,0,1,1					//Formato Paquete a enviar (event Report) bit1- Altitud
AT$APP PARAM 772,0,8,8					//bit3 = Comm Status
AT$APP PARAM 772,0,16,16				//bit4 = carrier info
AT$APP PARAM 772,0,32,32				//bit5 = I/O status
AT$APP PARAM 772,0,128,128				//bit7 = extended options (bit 9-11) nro de acumm a reportar
AT$APP PARAM 772,0,512,512				//bit9  (101)= 3 acumuladores	(512+2048)
AT$APP PARAM 772,0,1024,1024			//bit10 (011)= 8 acumuladores   (512+1024)
AT$APP PARAM 773,0,1,1					//Formato Paquete log (idem anterior)
AT$APP PARAM 773,0,8,8
AT$APP PARAM 773,0,16,16
AT$APP PARAM 773,0,32,32
AT$APP PARAM 773,0,128,128
AT$APP PARAM 773,0,512,512
AT$APP PARAM 772,0,1024,1024

//Perfiles de tiempo-distancia
AT$APP PARAM 262,0,30					//Perfil de tiempo y Distancia 0 (Tiempo=30 seg.)
AT$APP PARAM 263,0,800					//Perfil de tiempo y Distancia 0 (Distancia=800 m )
AT$APP PARAM 264,0,60					//Perfil de tiempo y Distancia 0 (Heading=60º)
AT$APP PARAM 262,1,120					//Perfil de tiempo y Distancia 1 (Tiempo)	Este perfil se activa cuando no hay COMM para alargar el tiempo entre reportes
AT$APP PARAM 263,1,4000					//Perfil de tiempo y Distancia 1 (Distancia) y tener mas tiempo sin llenar el LOG
AT$APP PARAM 264,1,180					//Perfil de tiempo y Distancia 1 (Direccion)

AT$APP PARAM 2560,0,0					//Reset Acumulador 0
AT$APP PARAM 2560,1,0					//Reset Acumulador 1
AT$APP PARAM 2560,2,0					//Reset Acumulador 2
AT$APP PARAM 2560,3,0					//Reset Acumulador 3
AT$APP PARAM 2560,4,0					//Reset Acumulador 4
AT$APP PARAM 2560,5,0					//Reset Acumulador 5
AT$APP PARAM 2560,6,0					//Reset Acumulador 6

//Tension Umbral Bat.Interna y ppal.
AT$APP PARAM 266,0,3700					//Limite Acumulador 0 = 3.7 V (Bateria interna)
AT$APP PARAM 266,1,2000					//Limite Acumulador 1 = 2.0 V (Bateria principal, desconexion de bateria!)

//Configuracion de los TIMERS:
AT$APP PARAM 265,0,1800					//Timer 0 = 30 min
AT$APP PARAM 265,1,900					//Timer 1 = 15 min
AT$APP PARAM 265,2,1860					//Timer 2 = 31 min
AT$APP PARAM 265,3,172800				//Timer 3 = 48 hs
AT$APP PARAM 265,4,300					//Timer 4 = 5 min
AT$APP PARAM 265,5,20					//Timer 5 = 20 s (Delay para el EV 21)
AT$APP PARAM 265,6,300					//Timer 6 = 5 min
AT$APP PARAM 265,7,2400					//Timer 7 = 40 min
AT$APP PARAM 265,8,3600					//Timer 8 = 1 hora
AT$APP PARAM 265,9,600					//Timer 9 = 10 min
AT$APP PARAM 265,10,60					//Timer 10 = 1 min (Delay para el EV 22)
AT$APP PARAM 265,14,240 				//Timer 14 = 4 min
AT$APP PARAM 265,15,120 				//Timer 15 a 120 s
//
// Detalle del Power management para la bateria interna, este se activa con la Inp6 que indica power external/internal:
//			
//			!<- Inp6=1, al volver a 0 se desactiva este esquema (power external)
// 			|  -Timer7-       -Timer8-	 Timer9  -Timer8-  Timer9	
// Activo-> |-------------|		        |-----| 		   |-----|		  .....
// Sleep -> |		      |_____________|	  |____________|     |________.....mientras haya carga suficiente para funcionar
//          !_No ext.batt.!_(set flag3)
//
//  Notas:  - Solo se va a dormir si NO se esta moviendo.
//       	- El LMU se despierta por un cambio en cualquier entrada o movimiento por acelerometro MEMS.

//Conf. Hora Local, Healtcheck, etc.:
AT$APP PARAM 1030,0,1,0					//Hora local +0
AT$APP PARAM 1030,0,2,0
AT$APP PARAM 1030,0,4,0
AT$APP PARAM 1030,0,8,0
AT$APP PARAM 1030,0,16,0
AT$APP PARAM 267,0,21600				//Hora del Healtcheck 3:00 AM (UTC+ 6Horas = 3 A.M. Asuncion (verano))
AT$APP PARAM 268,0,0					//Healtcheck una vez al día
AT$APP PARAM 267,1,10620				//Hora de Despertar al equipo (02:57 AM)
AT$APP PARAM 268,1,0					//Se despieta una vez al día

//Exceso de velocidad:
AT$APP PARAM 257,0,2360					//Exceso de velocidad 2360 cm/s = 85 Km/h
AT$APP PARAM 259,0,10					//Antirrebote 10s
AT$APP PARAM 260,0,180					//Duracion minima para generar exceso (3 min)

//Configuracion del acelerometro 3D para driver behiavior & Impacto:
AT$APP PARAM 281,0,1			//Fast acceleration (profile 0)
AT$APP PARAM 281,1,2			//Braking (profile 1)
AT$APP PARAM 281,2,3			//Left turn (profile 2)
AT$APP PARAM 281,3,4			//Right turn (profile 3)
AT$APP PARAM 281,4,133			//Profile 4 -> Vertical UP, fast, filtered 8 samples not 16 (5 + Bit7) 1000 0101 = 133d
AT$APP PARAM 283,0,180			//Acceleration threshold (profile 0, accel) = 180 cm/s/s (Calamp recomienda 250 en su ej.)
AT$APP PARAM 283,1,210			//Acceleration threshold (profile 1, braking) = 210 cm/s/s (recom: 330)
AT$APP PARAM 283,2,350			//Acceleration threshold (profile 2, left) = 350 cm/s/s	(recom: 350)
AT$APP PARAM 283,3,350			//Acceleration threshold (profile 2, right) = 350 cm/s/s (recom:350)
AT$APP PARAM 283,4,55			//Vertical UP threshold(empedrado o bumps)  = 55 cm/s/s
AT$APP PARAM 284,0,1500			//1500 mseg minimum (profile 0, accel)
AT$APP PARAM 284,1,1500			//1500 mseg minimum (profile 1, braking)
AT$APP PARAM 284,2,2000			//2000 mseg minimum (profile 2, left)
AT$APP PARAM 284,3,2000			//2000 mseg minimum (profile 2, right)
AT$APP PARAM 284,4,200			//Profile 4 Minimun duration (200ms)
AT$APP PARAM 285,0,30000		//30 seg (max.)
AT$APP PARAM 285,1,30000		//   "     "
AT$APP PARAM 285,2,30000		//   "     "
AT$APP PARAM 285,3,30000		//   "     "
AT$APP PARAM 285,4,30000		//   "     "
AT$APP PARAM 1024,54,2,2		//S-Reg 174 = 2  (Required for accurate calibration)
AT$APP PARAM 1024,55,67,67		//S-Reg 175 = 67 (Required for accurate calibration)
AT$APP PARAM 896,0,3			//Mode (0=Sensor, 1=Impact Only, 2=Motion Only, 3=Impact & Motion) 	
AT$APP PARAM 897,0,0,207		//Impact force 0xCF=207 = 8.640 G's (default)
AT$APP PARAM 898,0,0			//Impact filter duration x10ms (10 ms, recomendado)
AT$APP PARAM 899,0,50			//Motion period - Frecuencia de consulta del sensor (0x32 = 50ms)
AT$APP PARAM 900,0,10			//Motion interval, defines the number of consecutive sensor readings above or below the acceleration threshold value needed to change motion status 
AT$APP PARAM 901,0,1000			//Motion threshold (0x3E8 = 1000, recomendado)

//Inputs debounce:
at$app param 270,1,2			//Input 1 debounce (3 seg.)
AT$APP PARAM 271,1,1			//Delay Input High timer (In 1, 1 seg)
AT$APP PARAM 272,1,1			//Delay Input Low timer (In 1, 1 seg)
at$app param 270,2,2			//Input 2 debounce (3 seg.)
AT$APP PARAM 271,2,1			//Delay Input High timer (In 2, 1 seg)
AT$APP PARAM 272,2,1			//Delay Input Low timer (In 2, 1 seg)
at$app param 270,6,5			//Inp 6 = Pwr_external (LOW) / Internal_batt (HIHG)		
at$app param 270,7,240			//Inp 7 = bateria baja (debounce de 4 minutos) el Vthreshold es de 3.5V
AT$APP PARAM 271,7,240			//Delay Input High timer (In 7, delay de 4 min)

//GEO-ZONAS:
AT$APP GZ ERASE							//Borra datos de Zonas
AT$APP PARAM 261,0,4269703264,4237245030,400,400,1,20		//Zona 0 (Falcon)
AT$APP PARAM 261,1,4269457882,4240361105,400,400,1,20		//Zona 1 (CDE)
AT$APP PARAM 261,2,4267603841,4239113558,400,400,1,20		//Zona 2 (Encarnacion)
AT$APP PARAM 261,3,4269623739,4237323695,400,400,1,20		//Zona 3 (Ita Enramada 1)
AT$APP PARAM 261,4,4292429964,4237316140,400,400,1,20		//Zona 4 (Ita Enramada 2)
AT$APP PARAM 261,5,4272418925,4239262914,1500,1500,1,20		//Zona 5 (P.J.Caballero)
AT$APP PARAM 261,6,4272961569,4288905311,400,400,1,20		//Zona 6 (Mcal. Estigarribia)
AT$APP PARAM 261,7,4044396667,3719609444,400,400,1,20		//Zona 7 (Chaco)

//Comienzo del Script PEG:
//Formato de los eventos PEG: 512,Numero_de_linea,Trigger,Trigger_modif,Cond_1,Cond_1_modif.,Action,Action_modif.,Cond_2,Cond_2_modif.

AT$APP PARAM 512,0,15,0,0,0,20,0,0,0		//Ignicion ON -> Reset GPS
AT$APP PARAM 512,1,15,0,0,0,55,0,0,0		//Ignicion ON -> Reset Modem
AT$APP PARAM 512,2,15,0,0,0,47,0,0,0	   	//Ignition ON -> Inicia muestreo A/D 0 en Acumulador 1 (Bat. Principal)
AT$APP PARAM 512,3,15,0,0,0,47,81,0,0		//Ignition ON -> Inicia muestreo A/D 5 en Acumulador 0 (0x50=80d, AD_5 in Accum0, Bat. Interna)
//AT$APP PARAM 512,4,5,6,0,0,47,51,0,0		//Inp 6 Low (Pwr_external) -> Inicia muestreo A/D 3 en Acumulador 3 (0x33=51d , uP Temp?)
//AT$APP PARAM 512,5,5,6,0,0,47,100,0,0		//Inp 6 Low (Pwr_external) -> Inicia muestreo A/D 6 en Acumulador 4 (0x64=100d, Temp Sensor?)
AT$APP PARAM 512,6,5,6,0,0,27,2,0,0			//Inp 6 Low (Pwr_external) -> Inicia conteo Odometro en Acumulador 2

//Reportes cada 30 segundos (perfil de tiempo-distancia 0):
AT$APP PARAM 512,7,17,0,5,0,1,0,0,0			//Perfil Tiempo/Distancia y conexion GPRS -> Envía reporte al servidor con ACK (Ev 0)

//Perdida de señal GPRS:
AT$APP PARAM 512,8,14,0,17,1,17,6,0,0		//Pierde señal GPRS y Bandera 1 = 0 -> Timer 6 cuenta una vez (5 minutos)
AT$APP PARAM 512,9,18,6,17,1,6,0,0,0		//Timer 6 llegó a 0 y Bandera 1 = 0 -> Detiene Perfil de Tiempo y Distancia
AT$APP PARAM 512,10,18,6,17,1,7,1,0,0		//Timer 6 llegó a 0 y Bandera 1 = 0 -> Iniciar Perfil de Tiempo y Distancia 1 (mas largo para ahorrar espacio en LOG)
AT$APP PARAM 512,11,17,0,6,0,2,30,0,0		//Perfil Tiempo/Distancia y sin conexion GPRS -> Envía reporte al LOG (EV 30)
AT$APP PARAM 512,12,13,0,0,0,3,0,0,0		//Toma señal GPRS -> Envia datos del LOG al servidor
AT$APP PARAM 512,13,13,0,17,1,6,0,0,0		//Toma señal GPRS y Bandera 1 = 0 -> Detiene Perfil de Tiempo y Distancia
AT$APP PARAM 512,14,13,0,17,1,7,0,0,0		//Toma señal GPRS y Bandera 1 = 0 -> Iniciar Perfil de Tiempo y Distancia 0
AT$APP PARAM 512,15,13,0,17,1,30,6,0,0		//Toma señal GPRS y Bandera 1 = 0 -> Para e inicializa Timer (Timer 6)

//Parado (NotMoving):
AT$APP PARAM 512,16,43,0,0,0,13,0,0,0		//No se mueve -> Inicia conteo Timer 0 (30 min.)
AT$APP PARAM 512,17,18,0,17,0,6,0,0,0		//Timer 0 = 0 y Bandera 0 = 0 (no se mueve) -> Detiene Perfil de Tiempo y Distancia
AT$APP PARAM 512,18,18,0,17,0,31,1,0,0		//Timer 0 = 0 y Bandera 0 = 0 -> Bandera 1 = 1
AT$APP PARAM 512,19,18,0,5,0,1,11,17,0		//Timer 0 = 0 y Tiene señal GPRS y Bandera 0 = 0 -> Envía reporte al servidor con ACK (Ev 11)
AT$APP PARAM 512,20,18,0,6,0,2,31,17,0		//Timer 0 = 0 y Sin señal GPRS y Bandera 0 = 0 -> Envía reporte al LOG (Ev 31)
AT$APP PARAM 512,21,18,0,17,0,31,0,0,0		//Timer 0 = 0 y Bandera 0 = 0 -> Bandera 0 = 1 (Reporte cada 30 min)
AT$APP PARAM 512,22,18,0,5,0,1,0,16,0		//Timer 0 = 0 y Tiene señal GPRS y Bandera 0 = 1 -> Envía reporte al servidor con ACK (Ev 0, igual al perfil de Tiempo-dist pero c/30min )
AT$APP PARAM 512,23,42,0,0,0,30,0,0,0		//Se mueve -> Para e inicializa Timer (Timer 0)
AT$APP PARAM 512,24,42,0,0,0,32,1,0,0		//Se mueve -> Bandera 1 = 0
AT$APP PARAM 512,25,42,0,5,0,7,0,16,0		//Se mueve y Tiene señal GPRS y Bandera 0 = 1 -> Iniciar Perfil de Tiempo y Distancia 0
AT$APP PARAM 512,26,42,0,6,0,7,1,16,0		//Se mueve y Sin señal GPRS y Bandera 0 = 1 -> Iniciar Perfil de Tiempo y Distancia 1
AT$APP PARAM 512,27,42,0,0,0,32,0,0,0		//Se mueve -> Bandera 0 = 0

//Evento 21 (Desconexion de bat. ppal):
AT$APP PARAM 512,28,47,1,0,0,17,5,0,0		//Acumulador 1 menor a limite (Bat.ppal<2.0V) -> Timer 5 cuenta una vez
AT$APP PARAM 512,29,18,5,5,0,1,21,25,1		//Timer llegó a 0 (Timer 5) y Tiene señal GPRS y Acumulador 1 menor a limite -> Envía reporte al servidor con ACK (Ev 21) (Desconexion de Bat.Ppal)
AT$APP PARAM 512,30,18,5,6,0,2,21,25,1		//Timer llegó a 0 (Timer 5) y Sin señal GPRS y Acumulador 1 menor a limite -> Envía reporte al LOG (EV 21)
AT$APP PARAM 512,31,19,1,0,0,30,5,0,0		//Acumulador 1 mayor a limite -> Para e inicializa Timer (Timer 5)
//Evento 22 (Desconexion de alimentacion):
AT$APP PARAM 512,32,47,0,0,0,17,10,0,0		//Acumulador 0 (Bat. interna < 3.7V) menor a limite -> Timer 10 cuenta una vez (1 min para evitar oscilaciones)
AT$APP PARAM 512,33,18,10,5,0,1,22,25,0		//Timer 10 llegó a 0 y Tiene señal GPRS y Acumulador 0 menor a limite -> Envía reporte al servidor con ACK (Ev 22)
AT$APP PARAM 512,34,18,10,6,0,2,22,25,0		//Timer 10 llegó a 0 y Sin señal GPRS y Acumulador 0 menor a limite -> Envía reporte al LOG (EV 22)
AT$APP PARAM 512,35,19,0,0,0,30,10,0,0		//Acumulador 0 mayor a limite -> Para e inicializa Timer 10
//Evento 20 (Bateria interna baja):
AT$APP PARAM 512,36,4,7,5,0,1,20,0,0		//Inp7 High & Comm_Avail -> send_report Ev 20 (Bat. Interna baja)
AT$APP PARAM 512,37,4,7,6,0,2,20,0,0		//Inp7 High & !Comm_Avail -> log_report Ev 20 (el Vthreshold es de 3.5V)
//Evento 23 (Pulsador de Panico):
AT$APP PARAM 512,38,5,1,5,0,1,23,0,0		//Inp1=0 y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 23) - Pulsador de Panico
AT$APP PARAM 512,39,5,1,6,0,2,23,0,0		//Inp1=0 y Sin señal GPRS -> Envía reporte al LOG (Ev 23)

//Eventos de GEOFENCES (EV 50-58):
AT$APP PARAM 512,40,9,0,5,0,1,50,0,0		//Entra Zona 0 y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 50)
AT$APP PARAM 512,41,9,1,5,0,1,51,0,0		//Entra Zona 1 y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 51)
AT$APP PARAM 512,42,9,2,5,0,1,52,0,0		//Entra Zona 2 y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 52)
AT$APP PARAM 512,43,9,3,5,0,1,53,0,0		//Entra Zona 3 y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 53)
AT$APP PARAM 512,44,9,4,5,0,1,55,0,0		//Entra Zona 4 y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 55)
AT$APP PARAM 512,45,9,5,5,0,1,56,0,0		//Entra Zona 5 y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 56)
AT$APP PARAM 512,46,9,6,5,0,1,57,0,0		//Entra Zona 6 y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 57)
AT$APP PARAM 512,47,9,7,5,0,1,58,0,0		//Entra Zona 7 y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 58)
AT$APP PARAM 512,48,9,0,6,0,2,50,0,0		//Entra Zona 0 y Sin señal GPRS -> Envía reporte al LOG (Ev 50)
AT$APP PARAM 512,49,9,1,6,0,2,51,0,0		//Entra Zona 1 y Sin señal GPRS -> Envía reporte al LOG (Ev 51)
AT$APP PARAM 512,50,9,2,6,0,2,52,0,0		//Entra Zona 2 y Sin señal GPRS -> Envía reporte al LOG (Ev 52)
AT$APP PARAM 512,51,9,3,6,0,2,53,0,0		//Entra Zona 3 y Sin señal GPRS -> Envía reporte al LOG (Ev 53)
AT$APP PARAM 512,52,9,4,6,0,2,55,0,0		//Entra Zona 4 y Sin señal GPRS -> Envía reporte al LOG (Ev 55)
AT$APP PARAM 512,53,9,5,6,0,2,56,0,0		//Entra Zona 5 y Sin señal GPRS -> Envía reporte al LOG (Ev 56)
AT$APP PARAM 512,54,9,6,6,0,2,57,0,0		//Entra Zona 6 y Sin señal GPRS -> Envía reporte al LOG (Ev 57)
AT$APP PARAM 512,55,9,7,6,0,2,58,0,0		//Entra Zona 7 y Sin señal GPRS -> Envía reporte al LOG (Ev 58)

//Healtcheck report c/24 Horas:
AT$APP PARAM 512,56,20,0,0,0,1,1,0,0		//Hora del día (Hora 0) -> Envía reporte al servidor son ACK (Ev 1) (Healtcheck)

//Limite de Velocidad (EV 72):
AT$APP PARAM 512,57,7,0,5,0,1,72,0,0		//Exceso de velocidad (Limite 0) y Tiene señal GPRS -> Envía reporte al servidor con ACK (Ev 72)
AT$APP PARAM 512,58,7,0,6,0,2,72,0,0		//Exceso de velocidad (Limite 0) y Sin señal GPRS -> Envía reporte al LOG (Ev 72)

//Watchdog GPS:
AT$APP PARAM 512,59,12,0,1,0,13,1,0,0		//Pierde señal de GPS e Ignición ON -> Inicia conteo Timer 1
AT$APP PARAM 512,60,18,1,1,0,20,0,8,0		//Timer llegó a 0 (Timer 1) e Ignición ON y Sin señal de GPS -> Reset GPS
AT$APP PARAM 512,61,11,0,0,0,30,1,0,0		//Tomó señal de GPS -> Para e inicializa Timer (Timer 1)
//Watchdog COMM:
AT$APP PARAM 512,62,14,0,1,0,13,2,0,0		//Pierde señal de GPRS e Ignición ON -> Inicia conteo Timer 2
AT$APP PARAM 512,63,18,2,1,0,55,0,6,0		//Timer llegó a 0 (Timer 2) e Ignición ON y Sin señal GPRS -> Reset Modem
AT$APP PARAM 512,64,13,0,0,0,30,2,0,0		//Tomó señal de GPRS -> Para e inicializa Timer (Timer 2)

//Aceleracion, frenado y giros bruscos (EV 79-81):
AT$APP PARAM 512,65,71,0,5,0,1,79,0,0		//aceleration event detected (0,accel) & COMM -> Send report (Ev 79)
AT$APP PARAM 512,66,71,0,6,0,2,79,0,0		//aceleration event detected (0,accel) & !COMM -> LOG Report (Ev 79)
AT$APP PARAM 512,67,71,1,5,0,1,80,0,0		//aceleration event detected (1,break) & COMM -> Send report (Ev 80)
AT$APP PARAM 512,68,71,1,6,0,2,80,0,0		//aceleration event detected (1,break) & !COMM -> LOG Report (Ev 80)

at$app param 512,69,71,2,5,0,1,81,0,0		//aceleration event detected (2,left) -> Send report (81, giro, es indistinto si es izquierda o derecha)
at$app param 512,70,71,2,6,0,2,81,0,0		// ''        & no_comm                -> LOG report (81, giro) por lo que ambos envian el EV81
at$app param 512,71,71,3,5,0,1,81,0,0		//aceleration event detected (3,right)-> Send report (81, giro)
at$app param 512,72,71,3,6,0,2,81,0,0		// ''        & no_comm                -> LOG report (81. giro)

//Deteccion de empedrado o bumps (EV 82):
at$app param 512,73,71,4,5,0,1,82,0,0		//aceleration event detected (4,up)   -> Send report (82, baden)
at$app param 512,74,71,4,6,0,2,82,0,0		// ''        & no_comm                -> LOG report (82, baden)

//Deteccion de Impacto (EV 83):
at$app param 512,75,66,1,5,0,1,83,0,0		//66 Accelerometer Event(1=Impact; 0,2-255 reserved) -> Send report (83, Impacto)
at$app param 512,76,66,1,6,0,2,83,0,0		// ''        & no_comm                -> LOG report (83, Impacto)

// Power Management de 24 HS (con bat.ppal. OK se va a dormir a las 48 hs):
AT$APP PARAM 512,77,16,0,0,0,23,1,0,0		//Ignición OFF -> Habilita el power management (enable sleep)
AT$APP PARAM 512,78,16,0,0,0,17,3,0,0		//Ignición OFF -> Empieza conteo una vez de Timer (Timer 3 - 48hs)
AT$APP PARAM 512,79,18,3,5,0,1,8,0,0		//Timer 3 llegó a 0 & COMM_Avail  -> Send Report (EV 8 - Going to sleep for 24 hs)
AT$APP PARAM 512,80,18,3,6,0,2,8,0,0		//Timer 3 llegó a 0 & !COMM_Avail -> Log  Report (EV 8 - Going to sleep for 24 hs)
AT$APP PARAM 512,81,18,3,0,0,31,4,0,0		//Timer 3 a 0 (Timer 3) -> Bandera 4 = 1
AT$APP PARAM 512,82,18,3,0,0,24,1,0,0		//Timer llegó a 0 (Timer 3) -> Equipo duerme hasta hora del día (Hora 1, 2:57 AM)
AT$APP PARAM 512,83,2,0,2,0,17,4,16,4		//Despierta e Ignición OFF y Bandera 4 = 1 -> Empieza conteo una vez de Timer (Timer 4)
AT$APP PARAM 512,84,18,4,5,0,1,8,0,0		//Timer 4 llegó a 0 & COMM_Avail  -> Send Report (EV 8 - Going to sleep for 24 hs)
AT$APP PARAM 512,85,18,4,6,0,2,8,0,0		//Timer 4 llegó a 0 & !COMM_Avail -> Log  Report (EV 8 - Going to sleep for 24 hs)
AT$APP PARAM 512,86,18,4,16,4,24,1,0,0		//Timer llegó a 0 (Timer 4) y Bandera 4 = 1 -> Equipo duerme hasta hora del día (Hora 1)
AT$APP PARAM 512,87,15,0,10,6,23,0,0,0		//Ignición ON & Inp6 Low (pwr.external) -> Disable Sleep
AT$APP PARAM 512,88,15,0,0,0,30,3,0,0		//Ignición ON -> Para e inicializa Timer (Timer 3)
AT$APP PARAM 512,89,15,0,0,0,30,4,0,0		//Ignición ON -> Para e inicializa Timer (Timer 4)
AT$APP PARAM 512,90,15,0,16,4,32,4,0,0		//Ignición ON y Bandera 4 = 1 -> Bandera 4 = 0
AT$APP PARAM 512,91,42,0,10,6,23,0,0,0		//Moving &  Inp6 Low (pwr.external) -> Disable Sleep
AT$APP PARAM 512,92,42,0,0,0,30,3,0,0		//Se mueve -> Para e inicializa Timer (Timer 3)
AT$APP PARAM 512,93,42,0,0,0,30,4,0,0		//Se mueve -> Para e inicializa Timer (Timer 4)
AT$APP PARAM 512,94,42,0,16,4,32,4,0,0		//Se mueve y Bandera 4 = 1 -> Bandera 4 = 0

// Control del Blockar en Wake-up:
AT$APP PARAM 512,95,3,0,16,15,8,0,0,0		//Encendido o despierto y Bandera 15 = 1 -> Salida 0 = 1 (Block Car)
AT$APP PARAM 512,96,3,0,17,15,9,0,0,0		//Encendido o despierto y Bandera 15 = 0 -> Salida 0 = 0

//Power Management Bat.Interna (sin bat. ppal.):
AT$APP PARAM 512,97,4,6,0,0,23,1,0,0		//Inp6=1 (Power state=1: battery, 0:external) -> Habilita el sleep
AT$APP PARAM 512,98,4,6,0,0,17,7,0,0		//Entrada ON (Entrada 6) -> Timer 7 cuenta una vez
AT$APP PARAM 512,99,18,7,5,0,1,9,4,0		//Timer 7 = 0 & COMM_Avail & NotMoving  -> Send Report (EV 9 - Going to sleep)
AT$APP PARAM 512,100,18,7,6,0,2,9,4,0		//Timer 7 = 0 & !COMM_Avail & NotMoving -> Log  Report (EV 9 - Going to sleep)
AT$APP PARAM 512,101,18,7,0,0,31,3,4,0		//Timer 7 = 0 & NotMoving-> Set Bandera 3
AT$APP PARAM 512,102,18,7,0,0,22,8,4,0		//Timer 7 llegó a 0 & NotMoving -> Equipo duerme hasta que Timer 8 llegue a 0
AT$APP PARAM 512,103,2,0,16,3,17,9,0,0		//Despierta y Bandera 3 = 1 -> Timer 9 cuenta una vez
AT$APP PARAM 512,104,18,9,5,0,1,9,0,0		//Timer 9 llegó a 0 & COMM_Avail-> Send Report (EV 9 - Going to sleep)
AT$APP PARAM 512,105,18,9,6,0,2,9,0,0		//Timer 9 llegó a 0 & !COMM_Avail-> Log Report  (EV 9 - Going to sleep)
AT$APP PARAM 512,106,18,9,0,0,22,8,0,0		//Timer 9 llegó a 0 -> Equipo duerme hasta que Timer 8 llegue a 0
AT$APP PARAM 512,107,5,6,0,0,30,7,0,0		//Inp 6 Low (Pwr External) -> Para e inicializa Timer (Timer 7)
AT$APP PARAM 512,108,5,6,0,0,30,8,0,0		//Inp 6 Low (Pwr External) -> Para e inicializa Timer (Timer 8)
AT$APP PARAM 512,109,5,6,0,0,30,9,0,0		//Inp 6 Low (Pwr External) -> Para e inicializa Timer (Timer 9)
AT$APP PARAM 512,110,5,6,0,0,32,3,0,0		//Inp 6 Low (Pwr External) -> Clear Flag 3

AT$APP PARAM 512,111,3,0,4,0,13,0,0,0		//Power-up or Wake-up & NotMoving -> Start/restart Timer 0

//Jamming detection:
AT$APP PARAM 512,112,3,0,0,0,32,10,0,0 		//Wake-up or power-up -> Bandera 10 = 0
AT$APP PARAM 512,113,70,1,0,0,17,14,0,0 	//Trigger 70 (jamming detected?) -> Cuenta una vez el timer 14 (8 min)
AT$APP PARAM 512,114,18,14,50,0,1,36,0,0	//Timer 14 llegó a 0 y Condición 50 -> Envía reporte 36
AT$APP PARAM 512,115,18,14,50,0,2,36,0,0	//Timer 14 llegó a 0 y Condición 50 -> Envía reporte al LOG 36
AT$APP PARAM 512,116,18,14,50,0,8,2,0,0 	//Timer 14 llegó a 0 y Condición 50 -> Activa salida 2
AT$APP PARAM 512,117,18,14,50,0,31,10,0,0	//Timer 14 llegó a 0 y Condición 50 -> Bandera 10 = 1

AT$APP PARAM 512,118,70,0,16,10,17,15,0,0	//Trigger 70 y Bandera 10 = 1 -> Cuenta una vez el timer 15
AT$APP PARAM 512,119,18,15,178,0,9,2,0,0	//Timer 15 llegó a 0 y NOT Condición 50 -> Desactiva salida 2
AT$APP PARAM 512,120,18,15,178,0,1,37,0,0	//Timer 15 llegó a 0 y NOT condición 50 -> Envía reporte 37
AT$APP PARAM 512,121,18,15,178,0,32,10,0,0	//Timer 15 llegó a 0 y NOT Condición 50 -> Bandera 10 = 0

//AT$APP PARAM 512,xxx,5,6,0,0,47,37,0,0		//Inp 6 Low (Pwr_external) -> Inicia muestreo A/D 2 en Acumulador 5 (0x25=37d , VantGPS)

at$app PEG ACTION 32 0						//Clear Flag 0, no son borradas por "DEFAULT ALL" e indican el estado de la aplicacion (maquina de estados)
at$app PEG ACTION 32 1						//Clear Flag 1

AT$APP LOG CLEAR							//Borra el LOG (por si es un equipo reciclado!)
AT$APP REBOOT								//resetea el equipo (necesario en casos de reciclar equipos para que tome el APN correcto)
